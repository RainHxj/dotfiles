#
# A simple theme loosely based on afowler.
#
# Authors:
#   Martin Andre <martin.andre@gmail.com>
#

function prompt_mandre_precmd () {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_mandre_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_mandre_precmd

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{red}❮%f%b%F{red}❮❮%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:action' format ' +%s'
  zstyle ':prezto:module:git:info:branch' format '%b'
  # zstyle ':prezto:module:git:info:commit' format ' %F{white}%.7c%f'
  zstyle ':prezto:module:git:info:indexed' format '+'
  zstyle ':prezto:module:git:info:unindexed' format '*'
  zstyle ':prezto:module:git:info:keys' format 'rprompt' ' %F{yellow}‹%b%s%i%I›%f'

  PROMPT='${SSH_TTY:+"%n@%m %B%F{blue}::%f%b "}%F{green}%2~%f${git_info[rprompt]} %(!.%B%F{red}#%f%b.%B%F{blue}»%f%b) '
  # PROMPT='${SSH_TTY:+"%n@%m %B%F{blue}::%f%b "}%F{green}%2~%f %(!.%B%F{red}#%f%b.%B%F{blue}»%f%b) '
  RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %F{red}%? %B↵%b%f)${VIM:+" %B%F{green}V%f%b"}${MC_SID:+" %B%F{blue}☪%f%b"}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_mandre_setup "$@"

